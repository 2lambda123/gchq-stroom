apply plugin: 'nu.studer.jooq'

ext.moduleName = 'stroom.query.field.impl.db.jooq'

dependencies {
    implementation libs.jakarta_annotation_api
    implementation libs.jooq

    runtimeOnly libs.mysql_connector_java
    jooqGenerator libs.mysql_connector_java
}

// Configure jooq plugin to generate code
jooq {
    version = versions.jooq
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration

            // Disable JOOQ code generation on build
            generateSchemaSourceOnCompilation = false

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3307/stroom?useUnicode=yes&characterEncoding=UTF-8'
                    user = 'stroomuser'
                    password = 'stroompassword1'
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'stroom'
                        // Add anything you want included in generation below, whitespace ignored
                        // and comments allowed. Each one is a java regex
                        includes = 'field_source|field_info'
                        // We don't want to include flyway versioning
                        excludes = 'field_schema_history'
                        // Specify 'version' for use in optimistic concurrency control
                        recordVersionFields = 'version'
                        // Treat some tinyint columns as booleans
                        forcedTypes {
                            forcedType {
                                name = 'BOOLEAN'
                                includeExpression = '' +
                                        '.*\\.field_info\\.indexed|' +
                                        '.*\\.field_info\\.stored|' +
                                        '.*\\.field_info\\.term_positions|' +
                                        '.*\\.field_info\\.case_sensitive|' +
                                        '.*\\.field_info\\.queryable'
                                // see https://github.com/jOOQ/jOOQ/issues/9405
                                includeTypes = '(?i:tinyint)(\\(1\\))?'
                            }
                        }
                    }
                    target {
                        packageName = 'stroom.query.field.impl.db.jooq'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}
