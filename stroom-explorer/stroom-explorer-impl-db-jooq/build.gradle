apply plugin: 'nu.studer.jooq'

ext.moduleName = 'stroom.explorer.impl.db.jooq'

dependencies {
    compile libs.javax_annotation
    permitUnusedDeclared libs.javax_annotation
    compile libs.jooq

    jooqRuntime libs.mysql_connector_java

    // The following are required to put JAXB on the JOOQ classpath for the purpose of JOOQ code generation in J9+.
    // Later versions of the JOOQ Gradle plugin should make this unnecessary.
    jooqRuntime 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    jooqRuntime 'com.sun.xml.bind:jaxb-impl:2.3.0.1'
    jooqRuntime 'javax.activation:activation:1.1.1'
    jooqRuntime 'javax.xml.bind:jaxb-api:2.3.0'
}

def propNameJooqGeneration = 'jooqGeneration'

// to run the jooq code generation add the following to the gradle command arguments
// -PjooqGeneration=true
if (getPropertyOrDefault(propNameJooqGeneration, "false") == "true") {
    configure(project) {
        //configure jooq plugin to generate code
        jooq {
            version = versions.jooq
            edition = 'OSS'
            sample(sourceSets.main) {
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3307/stroom?useUnicode=yes&characterEncoding=UTF-8'
                    user = 'stroomuser'
                    password = 'stroompassword1'
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'stroom'
                        // Add anything you want included in generation below
                        includes = 'explorer_node|explorer_path'
                        // We don't want to include flyway versioning
                        excludes = 'explorer_schema_version'
                    }
                    target {
                        packageName = 'stroom.explorer.impl.db.jooq'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}
