buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath group: 'gchq', name: 'urlDependencyPlugin', version: 'v0.1'
        classpath 'ca.cutterslade.gradle:gradle-dependency-analyze:1.1.0'
    }
}
apply plugin: 'gchq.urldependencies'

ext.versions = [
        stroom: '6.0-beta.1-SNAPSHOT',
        stroomQueryApi: 'stroom-query-api-v0.1.0',
        stroomQueryApiSources: 'stroom-query-api-v0.1.0-sources',
        stroomQueryCommon: 'stroom-query-common-v0.1.0',
        stroomExpression: 'stroom-expression-v0.1.0',
        eventLogging: 'event-logging-v3.1.0',
        hadoopHdfsShaded: 'hadoop-hdfs-shaded-v2.6.4-all',
        hadoopCommonShaded: 'hadoop-common-shaded-v2.6.4-all'
]

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    group = 'stroom'
    version = versions.stroom
}

subprojects {
    apply plugin: 'java'
    
    //analyze missing dependencies for the code base
    apply plugin: 'ca.cutterslade.analyze'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        all {
            exclude group: "org.slf4j", module: "slf4j-log4j12"
            exclude group: "log4j", module: "log4j"
            exclude module: 'xercesImpl'
        }
    }

    configurations {
        testArtifacts
        sourceArtifacts
    }
    task testJar(type: Jar) {
        baseName = "${project.name}-test"
        from sourceSets.test.output
    }
    task sources(type: Jar) {
        baseName = "${project.name}-source"
        from sourceSets.main.allSource
    }
    artifacts {
        testArtifacts testJar
        sourceArtifacts sources
    }

    task packageTests(type: Jar) {
        from sourceSets.test.output
        classifier = 'tests'
    }
    artifacts.archives packageTests

    repositories {
        mavenLocal()

        maven { url "http://repo.maven.apache.org/maven2" }
    }
}

task setupSampleData(type: GradleBuild) {
    tasks = [':stroom-integrationtest:setupSampleData']
}

project(':stroom-util') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':stroom-util-shared') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':stroom-entity-shared') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

urlDependencies {
    libs "libs"
    compile(versions.stroomQueryApi,
        'https://github.com/gchq/stroom-query/releases/download/v0.1.1/stroom-query-api-v0.1.1.jar')
    compile(versions.stroomQueryApiSources,
        'https://github.com/gchq/stroom-query/releases/download/v0.1.1/stroom-query-api-v0.1.1-sources.jar')
    compile(versions.eventLogging,
        'https://github.com/gchq/event-logging/releases/download/v3.1.0/event-logging-v3.1.0.jar')
    compile(versions.stroomQueryCommon,
        'https://github.com/gchq/stroom-query/releases/download/v0.1.1/stroom-query-common-v0.1.1.jar')
    compile(versions.stroomExpression,
        'https://github.com/gchq/stroom-expression/releases/download/v0.1.0/stroom-expression-v0.1.0.jar')
    compile(versions.eventLogging,
        'https://github.com/gchq/event-logging/releases/download/v3.1.0/event-logging-v3.1.0.jar')
    compile(versions.hadoopHdfsShaded,
        'https://github.com/gchq/hadoop-hdfs-shaded/releases/download/v2.6.4/hadoop-hdfs-shaded-v2.6.4-all.jar')
    compile(versions.hadoopCommonShaded,
        'https://github.com/gchq/hadoop-common-shaded/releases/download/v2.6.4/hadoop-common-shaded-v2.6.4-all.jar')
}
