def warDir = project(':stroom-app').file('src/main/resources/ui')
def outputDir = "${warDir}/stroom"

def gwtBuildDir = "${project.buildDir}/gwt"
def tmpDir = "${gwtBuildDir}/tmp"
def workDir = "${gwtBuildDir}/work"
def deployDir = "${gwtBuildDir}/deploy"
def extraDir = "${gwtBuildDir}/extra"

dependencies {
    compile project(':stroom-index-client')
    compile project(':stroom-statistics-client')
    compile project(':stroom-dashboard-client')
    compile project(':stroom-security-shared')
    permitUnusedDeclared project(':stroom-security-shared')
    compile project(':stroom-core-client')
    compile project(':stroom-security-client-api')
    compile project(':stroom-core-client-widget')
    compile project(':stroom-security-client')

    compile libs.gin
    compile libs.guice
    compile libs.gwt_dev
    permitUnusedDeclared libs.gwt_dev
    compile libs.gwt_user
    compile libs.gwtp_mvp_client

    //needed so gwt can compile the stroom-query code that contains swagger annotations
    compile "${libs.swagger_annotations}:sources"
    permitUnusedDeclared "${libs.swagger_annotations}:sources"
}

def gwtSourceDirs = []
parent.subprojects.each { p ->
    if (p.name != 'stroom-dashboard-gwt') {
        gwtSourceDirs.add(p.sourceSets.main.output.classesDir)
        gwtSourceDirs.addAll(p.sourceSets.main.java.srcDirs)
        gwtSourceDirs.addAll(p.sourceSets.main.resources.srcDirs)
    }
}
gwtSourceDirs.addAll(sourceSets.main.compileClasspath)

// We need to add the sources of GitHub dependencies.
gwtSourceDirs.add(files(urlDependencies.getAsFile(urlDependencies.getAsPath(urlLibs.stroomQueryApiSources))).getAsPath())
gwtSourceDirs.add(files(urlDependencies.getAsFile(urlDependencies.getAsPath(urlLibs.stroomQueryApi))).getAsPath())
gwtSourceDirs.add(files(urlDependencies.getAsFile(urlDependencies.getAsPath(urlLibs.stroomQueryCommon))).getAsPath())
gwtSourceDirs.add(files(urlDependencies.getAsFile(urlDependencies.getAsPath(urlLibs.stroomQueryCommonSources))).getAsPath())


//println "Dumping gwtSourceDirs contents"
//gwtSourceDirs.each { dir -> 
    //println dir
//}

//task addGwtDeps {
    //dependsOn classes

    //doLast {
        //println "Dumping gwtSourceDirs:"
        //configurations.compile.files.each { 
            //if (it.name.startsWith("swagger")) {
                //println "Adding ${it.name} to GWT classpath" 
                //gwtSourceDirs.add(it.getPath())
            //}
        //}
    //}
//}

//task gwtCompile(dependsOn: [classes, addGwtDeps], type: JavaExec) {
task gwtCompile(dependsOn: classes, type: JavaExec) {
    def module = 'stroom.app.App'

    doFirst {
        println 'Deleting GWT compiled output: ' + outputDir.toString()
        delete outputDir
        println 'Deleting GWT build dir: ' + gwtBuildDir.toString()
        delete gwtBuildDir

        println "Making directory gwtBuildDir $gwtBuildDir"
        file(gwtBuildDir).mkdirs()
        println "Making directory tmpDir $tmpDir"
        file(tmpDir).mkdirs()
        println "Making directory workDir $workDir"
        file(workDir).mkdirs()
        println "Making directory deployDir $deployDir"
        file(deployDir).mkdirs()
        println "Making directory extraDir $extraDir"
        file(extraDir).mkdirs()

        println "Dumping gwtSourceDirs"
        gwtSourceDirs.each {
            println "${it.toString()}"
        }
    }

    main = gwtCompilerProps.mainClass


    classpath {
        gwtSourceDirs
    }

    args =
        [
            '-logLevel', 'INFO',
            '-workDir', workDir,
            '-XfragmentCount', '5',
            '-optimize', '9',
            '-failOnError',
            '-localWorkers', gwtCompilerProps.workers,
            '-war', warDir,
            '-deploy', deployDir,
            '-extra', extraDir,
            '-strict',
            module                               // Your GWT module
        ]

    jvmArgs = ["-Djava.io.tmpdir=${tmpDir}"]
    minHeapSize = gwtCompilerProps.minHeap
    maxHeapSize = gwtCompilerProps.maxHeap
}

task gwtDraftCompile(dependsOn: classes, type: JavaExec) {
    def module = 'stroom.app.AppSuperDevModeChrome'

    doFirst {
        println 'Deleting GWT compiled output: ' + outputDir.toString()
        delete outputDir

        println 'Making GWT output directories'
        file(gwtBuildDir).mkdirs()
        file(tmpDir).mkdirs()
        file(workDir).mkdirs()
        file(deployDir).mkdirs()
        file(extraDir).mkdirs()
    }

    main = gwtCompilerProps.mainClass

    classpath {
        gwtSourceDirs
    }

    args =
        [
            '-logLevel', 'INFO',
            '-workDir', workDir,
            '-draftCompile',                        // Speeds up compile with 25%
            '-XfragmentCount', '0',
            '-optimize', '0',
            '-failOnError',
            '-localWorkers', gwtCompilerProps.workers,
            '-incremental',
            '-war', warDir,
            '-deploy', deployDir,
            '-extra', extraDir,
            '-strict',
            module                                  // Your GWT module
        ]

    jvmArgs = ["-Djava.io.tmpdir=${tmpDir}"]
    minHeapSize = gwtCompilerProps.minHeap
    maxHeapSize = gwtCompilerProps.maxHeap
}

task gwtSuperDevMode(dependsOn: classes, type: JavaExec) {
    def module = 'stroom.app.AppSuperDevModeChrome'

//    def dirs = []
//    parent.file(".").eachDir {it.eachDirMatch("src") {it.eachDirMatch("main") { it.eachDirMatch("java") {dirs << it}}}}
//    parent.file(".").eachDir {it.eachDirMatch("src") {it.eachDirMatch("main") { it.eachDirMatch("resources") {dirs << it}}}}
//    println dirs

    doFirst {
        println "Making directory gwtBuildDir $gwtBuildDir"
        file(gwtBuildDir).mkdirs()
        println "Making directory tmpDir $tmpDir"
        file(tmpDir).mkdirs()
        println "Making directory workDir $workDir"
        file(workDir).mkdirs()
        println "Making directory deployDir $deployDir"
        file(deployDir).mkdirs()
        println "Making directory extraDir $extraDir"
        file(extraDir).mkdirs()
    }

    main = 'com.google.gwt.dev.DevMode'

    classpath {
        gwtSourceDirs
    }

    args =
        [
            '-logLevel', 'INFO',
            '-workDir', workDir,
            '-failOnError',
            '-war', warDir,
            '-deploy', deployDir,
            '-extra', extraDir,
            '-strict',
            '-noserver',
            module                                  // Your GWT module
        ]

    jvmArgs = ["-Djava.io.tmpdir=${tmpDir}"]
    minHeapSize = gwtCompilerProps.devModeMinHeap
    maxHeapSize = gwtCompilerProps.devModeMaxHeap
}

task gwtClean {
    doLast {
        println 'Deleting GWT compiled output: ' + outputDir.toString()
        delete outputDir
        println 'Deleting GWT build dir: ' + gwtBuildDir.toString()
        delete gwtBuildDir
        println "Making directory gwtBuildDir $gwtBuildDir"
        file(gwtBuildDir).mkdirs()
        println "Making directory tmpDir $tmpDir"
        file(tmpDir).mkdirs()
    }
}

gwtCompile.onlyIf { !project.hasProperty('skipGWT') }
build.dependsOn('gwtCompile')
