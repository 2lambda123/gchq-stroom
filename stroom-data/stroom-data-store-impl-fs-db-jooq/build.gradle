apply plugin: 'nu.studer.jooq'

ext.moduleName = 'stroom.data.store.impl.fs.db.jooq'

dependencies {
    compile libs.javax_annotation
    permitUnusedDeclared libs.javax_annotation
    compile libs.jooq

    runtime libs.mysql_connector_java
    jooqGenerator libs.mysql_connector_java
}

// Configure jooq plugin to generate code
jooq {
    version = versions.jooq
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration

            // Disable JOOQ code generation on build
            generateSchemaSourceOnCompilation = false

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3307/stroom?useUnicode=yes&characterEncoding=UTF-8'
                    user = 'stroomuser'
                    password = 'stroompassword1'
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'stroom'
                        // Add anything you want included in generation below, whitespace ignored
                        // and comments allowed. Each one is a java regex
                        includes = 'fs_volume|fs_volume_state|fs_feed_path|fs_type_path|fs_meta_volume|fs_orphaned_meta_tracker'
                        // We don't want to include flyway versioning
                        excludes = 'fs_schema_history'
                        // Specify 'version' for use in optimistic concurrency control
                        recordVersionFields = 'version'
                        // Treat tinyint(1) columns as booleans
                        forcedTypes {
                            forcedType {
                                name = 'BOOLEAN'
                                types = '(?i:TINYINT\\(1\\))'
                            }
                        }
                    }
                    target {
                        packageName = 'stroom.data.store.impl.fs.db.jooq'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}
