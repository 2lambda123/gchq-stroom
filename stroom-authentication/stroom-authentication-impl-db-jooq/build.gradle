/*
 * Copyright 2017 Crown Copyright
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext.moduleName = 'stroom.auth.impl.db.jooq'
apply plugin: 'nu.studer.jooq'

dependencies {
    compile libs.jooq
    compile libs.jooq_codegen
    compile libs.jooq_meta
    compile libs.javax_annotation
    compile libs.jaxws_api

    jooqRuntime libs.mysql_connector_java
}

def propNameJooqGeneration = 'jooqGeneration'

// TODO: auth-into-stroom: don't think we need jooqGeneration here? Probably in main Stroom.

//to run the jooq code generation add the following to the gradle command arguments
//-PjooqGeneration=true
if (getPropertyOrDefault(propNameJooqGeneration, "false") == "true") {
    configure(project) {
        //configure jooq plugin to generate code
        jooq {
            version = '3.11.7'
            edition = 'OSS'
            authdb(sourceSets.main) {
                jdbc {
                    driver = 'com.mysql.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3307/auth'
                    user = 'authuser'
                    password = 'stroompassword1'
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'auth'
                        // Add anything you want included in generation below
                        includes = 'users|tokens|token_types|json_web_key'
                        // We don't want to include flyway versioning
                        excludes = 'schema_version'

                        forcedTypes {
                            // Force TINYINT to Booelan rather than Byte
                            forcedType {
                                name = 'BOOLEAN'
                                types = '(?i:TINYINT)'
                            }
                        }
                    }
                    target {
                        packageName = 'stroom.auth.db'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}


