apply plugin: 'nu.studer.jooq'

ext.moduleName = 'stroom.streamstore.meta.impl.db.gen'

dependencies {
    compile libs.jooq
    permitUnusedDeclared libs.jooq

    jooqRuntime libs.mysql_connector_java
}

def propNameJooqGeneration = 'jooqGeneration'

// to run the jooq code generation add the following to the gradle command arguments
// -PjooqGeneration=true
if (getPropertyOrDefault(propNameJooqGeneration, "false") == "true") {
    configure(project) {
        //configure jooq plugin to generate code
        jooq {
            version = versions.jooq
            edition = 'OSS'
            sample(sourceSets.main) {
                jdbc {
                    driver = 'com.mysql.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3307/stroom?useUnicode=yes&characterEncoding=UTF-8'
                    user = 'stroomuser'
                    password = 'stroompassword1'
                }
                generator {
                    name = 'org.jooq.util.DefaultGenerator'
                    database {
                        name = 'org.jooq.util.mysql.MySQLDatabase'
                        // Add anything you want included in generation below
                        includes = 'STRM|STRM_FEED|STRM_TYPE|STRM_ATR_KEY|STRM_ATR_VAL|STRM_PROCESSOR'
                        // We don't want to include flyway versioning
                        excludes = 'schema_version'
                    }
                    target {
                        packageName = 'stroom.streamstore.meta.impl.db'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}