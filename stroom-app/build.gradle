plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'application'
mainClassName = 'stroom.startup.App'

jar {
    manifest {
        attributes(
                "Implementation-Title": "Stroom",
                "Implementation-Version": version,
                "Main-Class" : "stroom.startup.App"
        )
    }
    archiveName 'stroom-app.jar'
}

import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

shadowJar {
    // Allows us to build fat jars with lots of files
    zip64 true

    // A transformer to merges files in META-INF/services
    mergeServiceFiles()

    // A transformer to merge spring's generated config files, e.g. spring.handlers and spring.schemas
    transform(ServiceFileTransformer) {
        include 'META-INF/spring.*'
    }
    archiveName 'stroom-app-all.jar'
}

configurations {
    all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.name == 'log4j') {
                details.useTarget "org.slf4j:log4j-over-slf4j:1.7.5"
            }
        }
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "log4j", module: "log4j"
    }

    // This means our integration tests get all the dependencies from our tests and we don't need to specify them twice.
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile urlDependencies.get(versions.stroomQueryApi)
    compile urlDependencies.get(versions.stroomQueryCommon)

    compile project(':stroom-core-server')
    compile project(':stroom-security-server')
    compile project(':stroom-dashboard-server')
    permitUnusedDeclared project(':stroom-dashboard-server')
    compile project(':stroom-index-server')
    compile project(':stroom-statistics-server')
    permitUnusedDeclared project(':stroom-statistics-server')

    compile project(':stroom-entity-shared')
    compile project(':stroom-index-shared')
    compile project(':stroom-util')
    compile project(':stroom-security-api')

    compile 'org.slf4j:slf4j-api:1.7.24'
    compile 'org.glassfish:javax.el:3.0.1-b08'
    permitUnusedDeclared 'org.glassfish:javax.el:3.0.1-b08'

    compile ("io.dropwizard:dropwizard-core:1.1.0" ) {
        exclude ( group: "org.glassfish.web", module: "javax.el" )
    }
    compile ("io.dropwizard:dropwizard-assets:1.1.0" ) {
        exclude ( group: "org.glassfish.web", module: "javax.el" )
    }

    compile 'io.dropwizard.metrics:metrics-annotation:3.1.2'
    compile 'io.dropwizard:dropwizard-jersey:1.0.6'
    compile 'io.dropwizard:dropwizard-jetty:1.0.6'
    compile 'io.dropwizard.metrics:metrics-healthchecks:3.1.2'

    compile 'org.springframework:spring-web:4.2.1.RELEASE'
    compile 'org.springframework:spring-beans:4.3.4.RELEASE'
    compile 'org.springframework:spring-context:4.3.4.RELEASE'

    compile 'org.apache.shiro:shiro-web:1.3.2'
    permitUnusedDeclared 'org.apache.shiro:shiro-web:1.3.2'
    compile 'org.apache.curator:curator-x-discovery:2.12.0'
    compile 'org.apache.curator:curator-framework:2.12.0'
    compile 'org.apache.curator:curator-client:2.12.0'

    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'com.google.guava:guava:20.0'
    compile 'org.glassfish.jersey.core:jersey-server:2.25'
    compile 'org.apache.shiro:shiro-core:1.3.2'
    compile 'org.eclipse.jetty:jetty-server:9.3.9.v20160517'
    compile 'org.eclipse.jetty:jetty-servlet:9.3.9.v20160517'

    integrationTestCompile 'junit:junit:4.12'
    integrationTestCompile 'org.mockito:mockito-core:2.0.2-beta'
    integrationTestCompile 'org.assertj:assertj-core:3.6.2'

}

sourceSets {
    // This lets us use different directories for our integration tests.
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}



// This task lets us run the actual integration tests.
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

// TODO: Integration tests currently require manual setup of resources, which means hey cannot be included in the build as automated tests.
// These lines pull the integration test task into our build process.
//check.dependsOn integrationTest
//integrationTest.mustRunAfter test

// This means the reports from our integration tests won't over-write the reports from our unit tests.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

run {
    args 'server', './dev.yml'
}
