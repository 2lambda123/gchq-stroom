plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'application'
mainClassName = 'stroom.startup.App'

jar {
    manifest {
        attributes(
                "Implementation-Title": "Stroom",
                "Implementation-Version": versions.stroom,
                "Main-Class" : "stroom.startup.App"
        )
    }
    archiveName 'stroom-app.jar'
}

import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

shadowJar {
    // Allows us to build fat jars with lots of files
    zip64 true

    // A transformer to merges files in META-INF/services
    mergeServiceFiles()

    // A transformer to merge spring's generated config files, e.g. spring.handlers and spring.schemas
    transform(ServiceFileTransformer) {
        include 'META-INF/spring.*'
    }
    archiveName 'stroom-app-all.jar'
}

configurations {
    all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.name == 'log4j') {
                details.useTarget "org.slf4j:log4j-over-slf4j:1.7.5"
            }
        }
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "log4j", module: "log4j"
    }

    // This means our integration tests get all the dependencies from our tests and we don't need to specify them twice.
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {

    compile project(':stroom-core-server')
    compile project(':stroom-security-server')
    compile project(':stroom-index-server')
    compile project(':stroom-statistics-api')
    permitUnusedDeclared project(':stroom-statistics-api') //not clear why it thinks this is unused
    compile project(':stroom-entity-shared')
    compile project(':stroom-index-shared')
    compile project(':stroom-util')
    compile project(':stroom-security-api')

    runtime project(':stroom-dashboard-server')
    runtime project(':stroom-statistics-server')

    compile urlDependencies.get(versions.stroomQueryApi)
    compile urlDependencies.get(versions.stroomQueryCommon)

    compile libs.curator_client
    compile libs.curator_framework
    compile libs.curator_x_discovery
    compile (libs.dropwizard_assets) {
        exclude ( group: "org.glassfish.web", module: "javax.el" )
    }
    compile (libs.dropwizard_core) {
        exclude ( group: "org.glassfish.web", module: "javax.el" )
    }
    compile libs.dropwizard_jersey
    compile libs.dropwizard_jetty
    compile libs.dropwizard_metrics_annotation
    compile libs.dropwizard_metrics_healthchecks
    compile libs.dropwizard_servlets
    compile libs.guava
    compile libs.jackson_annotations
    compile libs.javax_inject
    compile libs.javax_servlet_api
    compile libs.jersey_server
    compile libs.jetty_server
    compile libs.jetty_servlet
    compile libs.logback_classic
    compile libs.shiro_core
    compile libs.slf4j_api
    compile libs.spring_beans
    compile libs.spring_context
    compile libs.spring_web
    compile libs.vavr
    compile libs.ws_rs_api

    runtime libs.shiro_web
    runtime libs.javax_el
    runtime libs.javax_el_api

    integrationTestCompile libs.junit
    integrationTestCompile libs.mockito_core
    integrationTestCompile libs.assertj_core
    integrationTestCompile libs.dropwizard_testing
    integrationTestCompile libs.dropwizard_client
}

sourceSets {
    // This lets us use different directories for our integration tests.
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}



// This task lets us run the actual integration tests.
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

// TODO: Integration tests currently require manual setup of resources, which means hey cannot be included in the build as automated tests.
// These lines pull the integration test task into our build process.
//check.dependsOn integrationTest
//integrationTest.mustRunAfter test

// This means the reports from our integration tests won't over-write the reports from our unit tests.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

run {
    args 'server', './dev.yml'
}
