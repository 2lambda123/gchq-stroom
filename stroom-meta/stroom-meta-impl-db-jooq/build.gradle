apply plugin: 'nu.studer.jooq'

ext.moduleName = 'stroom.meta.impl.db.jooq'

dependencies {
    compile libs.javax_annotation
    permitUnusedDeclared libs.javax_annotation
    compile libs.jooq

    jooqGenerator libs.mysql_connector_java

//    // The following are required to put JAXB on the JOOQ classpath for the purpose of JOOQ code generation in J9+.
//    // Later versions of the JOOQ Gradle plugin should make this unnecessary.
//    jooqRuntime 'javax.activation:activation:1.1.1'
//    jooqRuntime 'javax.xml.bind:jaxb-api:2.3.0'
//    jooqRuntime 'com.sun.xml.bind:jaxb-core:2.3.0.1'
//    jooqRuntime 'com.sun.xml.bind:jaxb-impl:2.3.0.1'
}

// Configure jooq plugin to generate code
jooq {
    version = versions.jooq
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = true  // default (can be omitted)

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3307/stroom?useUnicode=yes&characterEncoding=UTF-8'
                    user = 'stroomuser'
                    password = 'stroompassword1'
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'stroom'
                        // Add anything you want included in generation below, whitespace ignored
                        // and comments allowed. Each one is a java regex
                        includes = 'meta_feed|meta_type|meta_processor|meta|meta_key|meta_val|meta_retention_tracker'
                        // We don't want to include flyway versioning
                        excludes = 'meta_schema_history'
                        // Specify 'version' for use in optimistic concurrency control
                        recordVersionFields = 'version'
                        // Treat tinyint(1) columns as booleans
                        forcedTypes {
                            forcedType {
                                name = 'BOOLEAN'
                                types = '(?i:TINYINT\\(1\\))'
                            }
                        }
                    }
                    target {
                        packageName = 'stroom.meta.impl.db.jooq'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}
