ext.moduleName = 'stroom.dashboard.gwt'

def resourcesDir = project(':stroom-app').file('src/main/resources')
def warDir = "${resourcesDir}/ui"
def outputDir = "${warDir}/dashboard"

def gwtBuildDir = "${project.buildDir}/gwt"
def tmpDir = "${gwtBuildDir}/tmp"
def workDir = "${gwtBuildDir}/work"
def deployDir = "${gwtBuildDir}/deploy"
def extraDir = "${gwtBuildDir}/extra"

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'javax.xml.bind') {
                details.useVersion "2.3.1"
            }
            if (details.requested.group == 'com.fasterxml.jackson.core') {
                details.useVersion "2.9.6"
            }
        }
    }
}

// Declare a source set just for our GWT compile that is separate from teh Java compilation source set.
sourceSets {
    gwtSource
}

dependencies {
    compile project(':stroom-core-client')
    compile project(':stroom-core-client-widget')
    compile project(':stroom-dashboard:stroom-dashboard-client')
    compile project(':stroom-query:stroom-query-api')
    compile project(':stroom-security:stroom-security-client')

    // These source deps are all needed for the GWT transpiling to javascript
    compile libs.stroomDocRef
//    compile libs.stroomQueryApi

    compile libs.gin
    compile libs.guice3
    compile libs.gwt_dev
    compile libs.gwt_user
    compile libs.gwtp_mvp_client
    compile libs.jackson_annotations
    compile libs.jaxb_api
    compile libs.swagger_annotations // needed so gwt can compile the stroom-query code that contains swagger annotations

    gwtSourceCompile libs.gin
    gwtSourceCompile libs.gwt_dev
    gwtSourceCompile libs.gwt_user
    gwtSourceCompile libs.gwtp_mvp_client
    gwtSourceCompile libs.jaxb_api
    gwtSourceCompile libs.swagger_annotations // needed so gwt can compile the stroom-query code that contains swagger annotations
    gwtSourceCompile libs.restygwt
    gwtSourceCompile libs.ws_rs_api

    gwtSourceCompile libs.stroomDocRef
    gwtSourceCompile "${libs.stroomDocRef}:sources"
//    gwtSourceCompile libs.stroomQueryApi
//    gwtSourceCompile "${libs.stroomQueryApi}:sources"
//    gwtSourceCompile libs.stroomQueryCommon
//    gwtSourceCompile "${libs.stroomQueryCommon}:sources"
}

// Create a list of source directories where source for GWT can be found.
def gwtSourceDirs = []
// Add all of the library sources from the gwtSource source set defined above.
gwtSourceDirs.addAll(sourceSets.gwtSource.compileClasspath)

// Define a list of projects that contain the source code we want to compile with GWT.
def projList = [
    project(':stroom-dashboard-gwt'),
    project(':stroom-core-client'),
    project(':stroom-core-client-widget'),
    project(':stroom-core-shared'),
    project(':stroom-dashboard:stroom-dashboard-client'),
    project(':stroom-security:stroom-security-client'),
    project(':stroom-security:stroom-security-client-api'),
    project(':stroom-util-shared')
]

// Add source from each of the defined projects.
projList.each { p ->
    gwtSourceDirs.addAll(p.sourceSets.main.output.classesDirs)
    gwtSourceDirs.addAll(p.sourceSets.main.java.srcDirs)
    gwtSourceDirs.addAll(p.sourceSets.main.resources.srcDirs)
}

task gwtCompile(dependsOn: classes, type: JavaExec) {
    dependsOn 'gwtClean'
    
    def module = 'stroom.dashboard.DashboardApp'

    doFirst {
        println 'Deleting GWT compiled output: ' + outputDir.toString()
        delete outputDir
        println 'Deleting GWT build dir: ' + gwtBuildDir.toString()
        delete gwtBuildDir

        println "Making directory gwtBuildDir $gwtBuildDir"
        file(gwtBuildDir).mkdirs()
        println "Making directory tmpDir $tmpDir"
        file(tmpDir).mkdirs()
        println "Making directory workDir $workDir"
        file(workDir).mkdirs()
        println "Making directory deployDir $deployDir"
        file(deployDir).mkdirs()
        println "Making directory extraDir $extraDir"
        file(extraDir).mkdirs()

        println "Dumping $gwtSourceDirs.size gwtSourceDirs"
        gwtSourceDirs.each {
            println "${it.toString()}"
        }
    }

    main = gwtCompilerProps.mainClass

    classpath {
        gwtSourceDirs
    }

    args =
        [
            '-logLevel', 'INFO',
            '-workDir', workDir,
            '-XfragmentCount', '5',
            '-optimize', '9',
            '-failOnError',
            '-localWorkers', gwtCompilerProps.workers,
            '-war', warDir,
            '-deploy', deployDir,
            '-extra', extraDir,
            '-strict',
            module                               // Your GWT module
        ]

    jvmArgs = ["-Djava.io.tmpdir=${tmpDir}"]
    minHeapSize = gwtCompilerProps.minHeap
    maxHeapSize = gwtCompilerProps.maxHeap
}

task gwtDraftCompile(dependsOn: classes, type: JavaExec) {
    def module = 'stroom.dashboard.DashboardAppSuperDevModeChrome'

    doFirst {
        println 'Deleting GWT compiled output: ' + outputDir.toString()
        delete outputDir

        println 'Making GWT output directories'
        file(gwtBuildDir).mkdirs()
        file(tmpDir).mkdirs()
        file(workDir).mkdirs()
        file(deployDir).mkdirs()
        file(extraDir).mkdirs()
    }

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        gwtSourceDirs
    }

    args =
        [
            '-logLevel', 'INFO',
            '-workDir', workDir,
            '-draftCompile',                        // Speeds up compile with 25%
            '-XfragmentCount', '0',
            '-optimize', '0',
            '-failOnError',
            '-localWorkers', '5',
            '-incremental',
            '-war', warDir,
            '-deploy', deployDir,
            '-extra', extraDir,
            '-strict',
            module                                  // Your GWT module
        ]

    jvmArgs = ["-Djava.io.tmpdir=${tmpDir}"]
    minHeapSize = '1G'
    maxHeapSize = '4G'
}

task gwtSuperDevMode(dependsOn: classes, type: JavaExec) {
    def module = 'stroom.dashboard.DashboardAppSuperDevModeChrome'

//    def dirs = []
//    parent.file(".").eachDir {it.eachDirMatch("src") {it.eachDirMatch("main") { it.eachDirMatch("java") {dirs << it}}}}
//    parent.file(".").eachDir {it.eachDirMatch("src") {it.eachDirMatch("main") { it.eachDirMatch("resources") {dirs << it}}}}
//    println dirs

    doFirst {
        println "Making directory gwtBuildDir $gwtBuildDir"
        file(gwtBuildDir).mkdirs()
        println "Making directory tmpDir $tmpDir"
        file(tmpDir).mkdirs()
        println "Making directory workDir $workDir"
        file(workDir).mkdirs()
        println "Making directory deployDir $deployDir"
        file(deployDir).mkdirs()
        println "Making directory extraDir $extraDir"
        file(extraDir).mkdirs()

        def GREEN='\033[1;32m'
        def NC='\033[0m'

        println """
        To run stroom in SuperDevMode:${GREEN}
          * Set appConfig.security.authenticationRequired to false in local.yml
          * Vist the code server (link further down) and copy the dev mode buttons to your browser bookmarks
          * Visit stroom at http://localhost:8080/stroom/ui (NOTE: http, not https)
          * Click 'Dev Mode On'${NC}
        """.stripIndent().trim()
    }

    main = 'com.google.gwt.dev.DevMode'

    classpath {
        gwtSourceDirs
    }

    args =
        [
            '-logLevel', 'INFO',
            '-workDir', workDir,
            '-failOnError',
            '-war', warDir,
            '-deploy', deployDir,
            '-extra', extraDir,
            '-strict',
            '-noserver',
            module                                  // Your GWT module
        ]

    jvmArgs = ["-Djava.io.tmpdir=${tmpDir}"]
    minHeapSize = gwtCompilerProps.minHeap
    maxHeapSize = gwtCompilerProps.maxHeap
}

task gwtClean {
    doLast {
//        println 'Deleting GWT unit cache: ' + unitCacheDir.toString()
//        delete unitCacheDir
        println 'Deleting GWT compiled output: ' + outputDir.toString()
        delete outputDir
        println 'Deleting GWT build dir: ' + gwtBuildDir.toString()
        delete gwtBuildDir
        println "Making directory gwtBuildDir $gwtBuildDir"
        file(gwtBuildDir).mkdirs()
        println "Making directory tmpDir $tmpDir"
        file(tmpDir).mkdirs()
    }
}
